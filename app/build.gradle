plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.jeroenmols.libraryexample"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        checkReleaseBuilds false
    }
    signingConfigs {
        release {
            storeFile file('release.keystore')
            storePassword 'password'
            keyAlias = 'alias'
            keyPassword 'password'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

afterEvaluate {
    preReleaseBuild.dependsOn(":lib:assembleRelease")
    preReleaseBuild.dependsOn(":lib:generatePomFileForAarPublication")
}

repositories {
    maven { url "$projectDir/libs" }
}

dependencies {
    debugImplementation project(':lib')

    def releaseVersion = project.hasProperty("external_version") ? project.external_version : "local"
    releaseImplementation "com.jeroenmols.lib:library:$releaseVersion"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
}